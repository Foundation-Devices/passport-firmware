# SPDX-FileCopyrightText: 2021 Foundation Devices, Inc. <hello@foundationdevices.com>
#
# SPDX-License-Identifier: GPL-3.0-or-later

# Install dependencies.
deps:
    @echo "TBD"

# Initialize development environment
init: deps
    git config core.hooksPath .githooks

# Lint only the code of the project
lint-code:
    @echo "TBD"

# Lint all of the project
lint: lint-code
    reuse lint

#
# Bootloader Commands
#

# release, debug, locked and production are possible values
default_rel := 'release'

# Build the bootloader (debug, release, locked or production)
build-bl rel=default_rel:
    @echo "\nBuilding Bootloader..."
    @cd boards/Passport/bootloader; make {{rel}}

    @echo "\nAppending secrets to the end..."
    @cd boards/Passport/bootloader; add-secrets -b arm/release/bootloader.bin -s secrets

    @echo "\nBootloader Build Complete"

# Clean the bootloader build
clean-bl:
    @echo "Cleaning Bootloader..."
    @cd boards/Passport/bootloader; make clean
    @echo "Bootloader Clean Complete"

# Launch OCD, run a command and then exit
run-ocd-command command:
    sudo /usr/local/bin/openocd -f stlink.cfg -c "adapter speed 1000; transport select hla_swd" -f stm32h7x.cfg -c "init; reset halt; {{command}}" -c "exit"

# Flash the release bootloader with the secrets appended to the end
flash-bl rel=default_rel: (build-bl rel)
    just run-ocd-command "flash write_image erase boards/Passport/bootloader/arm/{{rel}}/bootloader-secrets.bin 0x8000000"
    just reset

# Flash the release bootloader with no secrets -- use to init. a new Secure Element
flash-bl-raw rel=default_rel: (build-bl rel)
    just run-ocd-command "flash write_image erase boards/Passport/bootloader/arm/{{rel}}/bootloader.bin 0x8000000"
    just reset

# Get the username for use below
user := `whoami`

# Read the "ROM Secrets" from Passport and save them to a file
save-secrets filename="boards/Passport/bootloader/secrets":
    just run-ocd-command "dump_image {{filename}} 0x0801FF00 256"
    # Running OCD as sudo makes the output file be owned by root, so switch it back to the user
    sudo chown {{user}}:{{user}} {{filename}}


#
# Firmware Commands
#

build:
    make BOARD=Passport

# Sign current firmware build with the user.pem key and set specified version
sign version="1.0.0": build
    @echo "\nAdding user signature...\n"
    @cosign -f build-Passport/firmware.bin -k ~/bin/keys/user.pem -v {{version}} > /dev/null

    @cosign -f build-Passport/firmware-key-user.bin -x
    @echo "\nSigning Complete!"

# Build, sign and flash the firmware with the specified version
flash version="1.0.0": (sign version)
    just run-ocd-command "flash write_image erase build-Passport/firmware-key-user.bin 0x8020000"
    just reset

# Install a recent Foundation-signed build
flash-foundation version="1.0.0":
    just run-ocd-command "flash write_image erase ../../releases/passport-fw-{{version}}.bin 0x8020000"
    just reset

# Reset the Passport
reset:
    just run-ocd-command "reset"

# Clean the firmware build
clean:
    make BOARD=Passport clean

# Calculate all hashes and format it all for GitHub release notes
hash filepath:
    #!/usr/bin/env bash
    filename=`basename {{filepath}}`

    # SHA256
    sha=`shasum -b -a 256 {{filepath}} | sed -rn 's/^(.*) .*$/\1/p'`
    echo -e "\nSHA256: $sha"
    echo -e "(shasum -b -a 256 $filename)\n"

    # MD5
    md5=`mdsum {{filepath}} | sed -rn 's/^(.*) .*$/\1/p'`
    echo "MD5: $md5"
    echo -e "(md5 $filename or mdsum $filename)\n"

    # Build Hash
    build_hash=`cosign -f {{filepath}} -x | sed -rn 's/^FW Build Hash:    (.*)$/\1/p'`
    echo -e "Build Hash: $build_hash"
    echo -e "(Developers Only)\n"

# Run all tests
test:
    @echo "TBD"

# Format the project's source code
fmt-py:
    #!/usr/bin/env bash
    pushd boards/Passport/modules
    files_to_fmt=`find . -path ./trezor-firmware -prune -false -o -name '*.py'`
    autopep8 --max-line-length=120 --in-place $files_to_fmt
    popd

fmt-c:
    #!/usr/bin/env bash
    pushd boards/Passport
    files_to_fmt=`find . -path ./trezor-firmware -prune -false -o -name '*.[c|h]'`
    clang-format-5.0 -i --style=file $files_to_fmt
    popd

fmt: fmt-py fmt-c



# Bootloader
